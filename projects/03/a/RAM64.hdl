// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/RAM64.hdl

/**
 * Memory of 64 registers, each 16 bit-wide. Out holds the value
 * stored at the memory location specified by address. If load==1, then 
 * the in value is loaded into the memory location specified by address 
 * (the loaded value will be emitted to out from the next time step onward).
 */

CHIP RAM64 {
    IN in[16], load, address[6];
    OUT out[16];

    PARTS:
    DMux8Way(
      in=load,
      sel=address[3..5],
      a=load-ram-0,
      b=load-ram-1,
      c=load-ram-2,
      d=load-ram-3,
      e=load-ram-4,
      f=load-ram-5,
      g=load-ram-6,
      h=load-ram-7
    );

    RAM8(in=in, load=load-ram-0, address=address[0..2], out=out-ram-0);
    RAM8(in=in, load=load-ram-1, address=address[0..2], out=out-ram-1);
    RAM8(in=in, load=load-ram-2, address=address[0..2], out=out-ram-2);
    RAM8(in=in, load=load-ram-3, address=address[0..2], out=out-ram-3);
    RAM8(in=in, load=load-ram-4, address=address[0..2], out=out-ram-4);
    RAM8(in=in, load=load-ram-5, address=address[0..2], out=out-ram-5);
    RAM8(in=in, load=load-ram-6, address=address[0..2], out=out-ram-6);
    RAM8(in=in, load=load-ram-7, address=address[0..2], out=out-ram-7);

    Mux8Way16(
      a=out-ram-0,
      b=out-ram-1,
      c=out-ram-2,
      d=out-ram-3,
      e=out-ram-4,
      f=out-ram-5,
      g=out-ram-6,
      h=out-ram-7,
      sel=address[3..5],
      out=out
    );
}